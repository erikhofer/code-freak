plugins {
  id 'org.jetbrains.kotlin.plugin.jpa' version '1.3.21'
  id 'org.springframework.boot' version '2.1.3.RELEASE'
  id 'org.jetbrains.kotlin.jvm' version '1.3.21'
  id 'org.jetbrains.kotlin.plugin.spring' version '1.3.21'
  id 'com.diffplug.gradle.spotless' version '3.19.0'
  id 'com.bertramlabs.asset-pipeline' version '3.0.8'
  id 'jacoco'
  id 'com.github.kt3k.coveralls' version '2.8.2'
  id 'ua.eshepelyuk.ManifestClasspath' version '1.0.0'
}

apply plugin: 'io.spring.dependency-management'

group = 'de.code_freak'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
  mavenCentral()
  jcenter()
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-aop'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.springframework.boot:spring-boot-starter-data-ldap'
  implementation 'org.springframework.boot:spring-boot-starter-mail'
  implementation 'org.springframework.boot:spring-boot-starter-security'
  implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect:2.3.0'
  implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
  implementation 'org.jetbrains.kotlin:kotlin-reflect'
  implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
  implementation 'com.bertramlabs.plugins:asset-pipeline-spring-boot:3.0.10'
  implementation 'com.bertramlabs.plugins:sass-asset-pipeline:3.0.9'
  implementation 'com.vladsch.flexmark:flexmark-all:0.40.32'
  implementation 'org.webjars:jquery:3.3.1'
  implementation 'org.webjars:popper.js:1.14.6'
  implementation 'org.webjars:bootstrap:4.3.1'
  implementation 'org.webjars:font-awesome:5.8.1'
  implementation 'com.spotify:docker-client:8.15.1'
  implementation 'org.apache.commons:commons-compress:1.18'
  implementation 'com.beust:klaxon:5.0.1'
  implementation 'io.sentry:sentry-logback:1.7.16'
  runtimeOnly 'org.springframework.boot:spring-boot-devtools'
  runtimeOnly 'org.postgresql:postgresql'
  runtimeOnly 'org.hsqldb:hsqldb'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.springframework.security:spring-security-test'
  testImplementation 'org.hamcrest:hamcrest:2.1'
  testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0"

  // workaround for https://github.com/spotify/docker-client/issues/1030
  implementation 'org.glassfish.jersey.inject:jersey-hk2:2.26'
  implementation 'org.glassfish.jersey.bundles.repackaged:jersey-guava:2.25.1'
}

compileKotlin {
  kotlinOptions {
    freeCompilerArgs = ['-Xjsr305=strict']
    jvmTarget = '1.8'
  }
}

compileTestKotlin {
  kotlinOptions {
    freeCompilerArgs = ['-Xjsr305=strict']
    jvmTarget = '1.8'
  }
}

bootJar {
  // include assets so they can be served dynamically in production
  from (fileTree("src/assets").files) {
    into 'META-INF/assets'
  }
}

spotless {
  kotlin {
    ktlint().userData(['indent_size': '2'])
  }
}

jacocoTestReport {
  reports {
    xml.enabled = true
    html.enabled = true
  }
}

coveralls {
  sourceDirs += ['src/main/kotlin']
}

springBoot {
  mainClassName = 'de.code_freak.codefreak.CodeFreakApplicationKt'
}

test {
  testLogging {
    events "failed"
    exceptionFormat "full"
  }
}

task vagrantTest(type: Test) {
  systemProperty "code-freak.docker.host", "http://localhost:2375"
}
